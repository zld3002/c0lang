// Printing ints in hex form
// use % cc0 -lstring -lconio
#include <string.h0>
#include <conio.h0>

string hexdigit(int i)
//@requires 0 <= i && i <= 0xF;
{
  if (0 <= i && i <= 9)
    return string_fromint(i);
  else if (i == 0xA) return "A";
  else if (i == 0xB) return "B";
  else if (i == 0xC) return "C";
  else if (i == 0xD) return "D";
  else if (i == 0xE) return "E";
  else if (i == 0xF) return "F";
  else return "?";
}

string hex(int n) {
  int j = n; string result = "";
  int i; 
  for (i = 0; i < 32/4; i++) {
    result = string_join(hexdigit(j & 0xF), result);
    j = j>>4;
  }
  return string_join("0x", result);
}

int main () {
  assert(string_equal("0xBCDEF012", hex(0xBCDEF012)));
  assert(string_equal("0x3456789A", hex(0x3456789A)));
  assert(string_equal("0xDEADBEEF", hex(0xdeadbeef)));
  assert(string_equal("0x8BADF00D", hex(0x8badf00d)));
  assert(string_equal("0xCAFEBABE", hex(0xcafebabe)));
  assert(string_equal("0x00C0FFEE", hex(0xc0ffee)));
  println("Tests passed!");
  return 0;
}
